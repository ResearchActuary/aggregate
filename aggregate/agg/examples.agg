# How to and Tutorial Examples
# ============================

# Basic Examples
agg Basic.Dice.Roll dfreq [1:6] dsev [1]                      note{The roll of a single dice.}
agg Basic.Twelve.Dice.Rolls dfreq [5] dsev [1:6]              note{Sum of the rolls of twelve die. Close to normal.}
agg Basic.Poisson.Dice.Rolls 5 claims dsev [1:6] poisson      note{The sum of the rolls of Poisson(5) die. Note mass at zero.}

agg Basic.Tweedie tweedie 100 1.1 1.0                         note{Tweedie distribution mean μ=100, p=1.1, and dispersion σ^2=1.0. Variance function is σ^2 μ^p}

# for portfolio construction: 10M sized books with "reasonable" severity curves
# IRS: used cycle adjusted numbers, no further adjustments
# RMI: see  \S\Teaching\2019-09\RMI3388\Notes\pdf\Archive\QA16_RMI3388_Tue-29-Oct-2019_cat_models.pdf
# SCS is a swag; last pages of 2021 cat report for EF3+ tornados shows 25-50/year; big
# SCS events 3-6 billion; https://www.aon.com/reinsurance/catastropheinsight/global-regional-losses.html?region=United%20States&type=insured
# shows avg / year about 25B, so mean severity is 25 / 37.5 = 2/3 = 666M
# sigma 1.75 of  makes 10B a 1 in 16 event; mu mean=666 eq np.log(666e6) - 1.75**2/2 = 18.785550228504665
agg US.Hurricane 1.79 claims 1e12 xs 0 sev exp(19.595) * lognorm 2.581 poisson  note{Based on NOAA reanalysis dataset and sample of hurricane loss. Source SJU RMI3388 Course.}
agg US.SCS      37.50 claims 50e9 xs 0 sev exp(18.785550228504665) * lognorm 1.75 poisson note{Judgmental based on fre3quency of EF4-5 tornadoes, AAL of 25B and extreme event loss of 10B}

# CVs based on sigmas of 1.5 for liab, 1.75 for prof and 1.25 for ppa
# Average severity selected judgmentally
agg InsCo.Commercial.Liability     10e6 loss 1e6 xs 0   sev lognorm 100e3 cv 3.77 mixed gamma 0.26 note{Source: CV from Aon IRS 10th ed, severity selected judgmentally.}
agg InsCo.Commercial.Professional  10e6 loss 5e6 xs 0   sev lognorm 850e3 cv 8.48 mixed gamma 0.27 note{Source: CV from Aon IRS 10th ed, severity selected judgmentally.}
agg InsCo.Personal.Auto            10e6 loss 300e3 xs 0 sev lognorm  45e3 cv 20.4 mixed gamma 0.13 note{Source: CV from Aon IRS 10th ed, severity selected judgmentally.}

# limits profile low and high limits and attaching book: 1/1, 4/1 and 5/5
# severity midway between liability and professional
agg InsCo.Commercial.Umbrella [6e6 3e6 1e6] loss [1e6 4e6 5e6] xs [1e6 1e6 5e6] sev lognorm 250e3 cv 5.00 mixed gamma 0.26 note{Source: CV from Aon IRS 10th ed, severity selected judgmentally.}

# property mixed severity property and cat, pass through mixing
# note on CV of betas: for high cv it becomes a zero / one variable, to the variance maxes out at p (1-p) where p is the mean
# thus the CV maxes out at 1/sqrt(p(1-p)). For a mean of 0.1 the variance equals 1/sqrt(0.09) = 0.3 and the cv is 0.3 / 0.1 = 3
# could limit profile too, but keep simple
# assumes full ITV
agg InsCo.Commercial.Property [7e6 3e6] loss 100e6 xs 0 sev 100e6 * beta 0.1 cv [1.25 2.5] mixed gamma 0.28 note{Source: CV from Aon IRS 10th ed, severity selected judgmentally.}
agg InsCo.Homeowners          [8e6 2e6] loss 650e3 xs 0 sev 650e3 * beta 0.1 cv [0.75 2.75] mixed gamma 0.37 note{Source: CV from Aon IRS 10th ed, severity selected judgmentally.}

# really a severity curve, but you can't mix severity
agg InsCo.Commercial.Auto.MED 1 claim sev [2.764e3 24.548e3 275.654e3 1.917469e6 10e6] * expon 1 wts [0.824796 0.159065 0.014444 0.001624, 0.000071] fixed note{Use log2=18 and bs=500 or apply limits. Source: Example from Mixed Exponential snippet and Similar Risks IME paper (2022).}

# PIR book case studies
# hints: reg_p=1, roe=0.10, bs=1, log2=8, padding=1
port PIR.Discrete note{PIR Discrete case study. Change 8 to 9 for the equal points example.}
    agg Discrete.X1 1 claim dsev [0 8 10] [1/2 1/4 1/4] fixed
    agg Discrete.X2 1 claim dsev [0 1 90] [1/2 1/4 1/4] fixed

# hints: reg_p=.999, roe=0.10, bs=1/64, log2=16, padding=1
port PIR.Tame note{PIR Tame case study. For reinsurance see text.}
    agg Tame.A 1 claim sev gamma  50 cv 0.10 fixed
    agg Tame.B 1 claim sev gamma  50 cv 0.15 fixed

# hints: reg_p=.999, roe=0.10, bs=1/64, log2=16, padding=1
port PIR.Cat.NonCat.Gross note{PIR Cat-Noncat gross case study.}
    agg CNC.NonCat 1 claim sev gamma    80 cv 0.15 fixed
    agg CNC.Cat 1 claim sev lognorm  20 cv 1.00 fixed

port PIR.Cat.NonCat.Net note{PIR Cat-Noncat net case study.}
    agg CNC.Net.NonCat 1 claim sev gamma    80 cv 0.15 fixed
    agg CNC.Net.Cat    1 claim sev lognorm  20 cv 1.00 fixed aggregate net of 79.64 xs 41.11

# hints: reg_p=.999, roe=0.10, bs=1/4, log2=19, padding=1
port PIR.HU.SCS.Gross note{PIR Hurricane SCS gross case study.}
    agg SCS 70 claims sev exp(-1.9**2 / 2)      * lognorm 1.9 poisson
    agg Hu   2 claims sev exp(-2.5**2/2)/(1/15) * lognorm 2.5 poisson


port PIR.HU.SCS.Net note{PIR Hurricane SCS net case study.}
    agg Net.SCS 70 claims sev    exp(-1.9**2 / 2)   * lognorm 1.9 poisson
    agg Net.Hu   2 claims sev exp(-2.5**2/2)/(1/15) * lognorm 2.5 occurrence net of 372.4 xs 40.25 poisson

port BODOFF1   note{Bodoff Thought Experiment No. 1}
    agg Bodoff.wind   dfreq [1] dsev [0, 99]  [0.80, 0.20]
    agg Bodoff.quake  dfreq [1] dsev [0, 100] [0.95, 0.05]

# port Weird.Example note{Example with unusual exeqa function.}
# portfolio.make_awkward
